// Main build script
//
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK
//        gradlew test                    Run local unit test
//        gradlew connectedAndroidTest    Run instrumented unit test

apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

kapt {
    correctErrorTypes = true // avoid unknown type replacing with NonExistentClass
}

// Last Git tag name on current branch
final GIT_TAG_NAME = "git describe --tags --abbrev=0".execute([], project.rootDir).text.trim()

// Total number of tags in the app
final GIT_TAG_COUNT = "git tag --list".execute([], project.rootDir).text.split("\n").size()

apply from: 'common.gradle'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    dataBinding.enabled = true

    defaultConfig {
        applicationId "cz.koto.kotiheating"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode GIT_TAG_COUNT
        versionName GIT_TAG_NAME
        aaptOptions.cruncherEnabled false
        //multiDexEnabled true
        resValue "string", "app_name", "KoTiHeating"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "env"
    //https://developer.android.com/studio/build/gradle-plugin-3-0-0-migration.html
    productFlavors {
        mock {
            dimension = "env"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
            resValue "string", "app_name", "KoTiHeating (Mock)"
        }
        production {
            dimension = "env"
            resValue "string", "app_name", "KoTiHeating"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        mock {
            java {
                srcDirs('src/server/kotlin')
            }
        }
        production {
            java {
                srcDirs('src/server/kotlin')
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOGS", "true"
            resValue "string", "app_name", "KoTiHeating DEBUG"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            aaptOptions.cruncherEnabled true
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            // Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
            ext.alwaysUpdateBuildId = false
        }


        release {
            buildConfigField "boolean", "LOGS", "false"

            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true

            proguardFile 'proguard-rules/proguard-square-retrofit2.pro'
            proguardFile 'proguard-rules/proguard-square-leakcanary.pro'
            proguardFile 'proguard-rules/proguard-square-okio.pro'
            proguardFile 'proguard-rules/proguard-glide.pro'
            proguardFile 'proguard-rules/proguard-gson.pro'
            proguardFile 'proguard-rules/proguard-square-okhttp3.pro'
            proguardFile 'proguard-rules/proguard-support-design.pro'
            proguardFile 'proguard-rules/proguard-support-v7-appcompat.pro'
            proguardFile 'proguard-rules/proguard-support-v7-cardview.pro'
            proguardFile 'proguard-rules/proguard-realm.pro'
            proguardFile 'proguard-rules/proguard-reactive.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }
}


repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    kapt "com.android.databinding:compiler:$android_gradle_version"
    implementation "com.android.databinding:library:$android_binding_version"
    implementation "com.android.databinding:adapters:$android_binding_version"

    //implementation "com.android.support:multidex:$multidex_version"

    // support library
    implementation "com.android.support.constraint:constraint-layout:$android_constraint_layout_version"
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:gridlayout-v7:$android_support_version"

	implementation "android.arch.lifecycle:extensions:$rootProject.arch_lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.arch_lifecycle_version"

	implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
	implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version@aar"

    // retrofit + okhttp + gson
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation("com.squareup.retrofit2:converter-gson:$retrofit_version") {
        transitive = true
        exclude module: 'gson'
    }
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation("com.squareup.retrofit2:retrofit-mock:$retrofit_version") {
        exclude group: 'com.squareup.retrofit2', module: 'retrofit'
    }

    // leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    implementation 'org.parceler:parceler-api:1.1.9'
    kapt 'org.parceler:parceler:1.1.9'

    /**
     *  UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST UNIT-TEST
     */
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "com.android.support.test:runner:$android_support_test_version"
    androidTestImplementation "com.android.support.test:rules:$android_support_test_version"
    /**
     *  UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST UI-TEST
     */
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    // Espresso core
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version") {
        exclude group: 'com.android.support'
    }

    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espresso_version") {
        exclude group: 'com.android.support'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$espresso_version") {
        exclude group: 'com.android.support'
    }
    androidTestImplementation "com.android.support:support-annotations:$android_support_version"
}
